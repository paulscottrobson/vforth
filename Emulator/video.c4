//
//	Colour Video Input/Out, Keyboard reading routines.
//

: height 16 ; : width 32 ; 														// screen dimensions.
: editarea 4 ; 																	// lines for edit area

: __cursor $variable ; 															// current cursor position.
: __colour $variable ; 															// current colour

: red 1 __colour ! ; : green 2 __colour ! ; : yellow 3 __colour ! ; 			// colour settings
: blue 4 __colour ! ; : magenta 5 __colour ! ; : cyan 6 __colour ! ; : white 7 __colour ! ;

: home 0 __cursor ! ; 															// home cursor 

: screen! 1 $hwio 2 $hwio ; 													// write to display (addr chcl -)
: __fillcopy over over screen! 													// tail recursive screen fill.
					dup height width * = #0if exit 1+ $loop  

: fillscreen 0 __fillcopy drop drop home ;  									// clear screen with character (fill - )
							
: clearScreen 32 fillscreen ; 													// clear screen ( - )						
	
: cursor width * + __cursor ! ;													// cursor position (x y -)

: __setVisibleCursor  __cursor @ screen! ; 										// Draw cursor in given colour (chcl -)

: __checkOff __cursor @ height editarea - width * >= #0if clearscreen ;			// Check if wrap needed.

: __printReturn 																// Print return (return - )
	r> drop __cursor @  width + $FFFF width - 1+ and __cursor ! __checkOff ;

: __printBackspace																// Print backspace (backspace - )
	r> drop __cursor @ width 1- and 0= #0if exit 								// abandon if can't back space
	-1 __cursor +! ;

: __printControl r> drop 														// Handle control keys (key - )
	31 and 
	dup 13 = #0if __printReturn
	dup 8 = #0if __printBackspace
	dup 12 = #0if clearscreen
	drop ;

: emit																			// Print character supports bs/cr (chcl -)
	dup 127 and 32 < #0if __printControl 										// If control, print them.
	__colour @ 7 and 256 * or 													// Colour them.
	__cursor @ screen! 1 __cursor +! __checkOff ; 								// write character out and advance.
;

: key 0 $hwio dup #0if exit drop $loop ;										// get a keystroke ( - key)

: keyWithCursor $5FF __setVisibleCursor key $720 __setVisibleCursor ;			// Get key with cursor display

: $__init_video clearscreen white ;
